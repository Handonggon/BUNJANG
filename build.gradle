buildscript {
	ext {
		swaggerVersion = "3.0.0"
		queryDslVersion = "5.0.0"
	}
}

plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id 'java'
	id 'war'
}

group = 'kr.co.study'
version = '0.0.1'
sourceCompatibility = '11'

configurations {
	customProvidedRuntime
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()     // 로컬 저장소 사용
    mavenCentral()   // 원격 저장소 사용
    jcenter()
    google()
}

dependencies {
	//implementation "org.springframework.boot:spring-boot-starter-hateoas"
	//implementation "org.springframework.boot:spring-boot-starter-actuator"
	//implementation "org.springframework.boot:spring-boot-starter-cache"
	//implementation "org.springframework.boot:spring-boot-starter-mail"
	implementation "org.springframework.boot:spring-boot-starter-web"
	//implementation "org.springframework.boot:spring-boot-starter-jdbc"
	implementation "org.springframework.boot:spring-boot-starter-aop"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-webflux"
	customProvidedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		exclude group: "org.junit.vintage", module: "junit-vintage-engine"
	}
	implementation "org.springframework.boot:spring-boot-starter-batch"
    implementation "org.springframework.boot:spring-boot-starter-quartz"

	//implementation "org.springframework.session:spring-session-core"
	compileOnly "org.projectlombok:lombok"
	annotationProcessor "org.projectlombok:lombok"
	developmentOnly "org.springframework.boot:spring-boot-devtools"

	// Thymeleaf
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'

	// Lucty XSS Filter
	implementation 'com.navercorp.lucy:lucy-xss-servlet:2.0.1'
	implementation 'com.navercorp.lucy:lucy-xss'

	// Utils
	implementation 'org.apache.commons:commons-text:1.10.0'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.apache.commons:commons-io:1.3.2'
	implementation 'org.apache.tika:tika-core:2.6.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind'

	// DB & JPA
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
	runtimeOnly 'mysql:mysql-connector-java'
	runtimeOnly 'com.oracle.database.jdbc:ojdbc6:11.2.0.4'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'
	
	// Swagger
	implementation "io.springfox:springfox-boot-starter:${swaggerVersion}"
	implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

	// QueryDSL
	implementation "com.querydsl:querydsl-core:${queryDslVersion}"
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	implementation "com.querydsl:querydsl-collections:${queryDslVersion}"
	annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	
	// JWT
	implementation "io.jsonwebtoken:jjwt:0.9.1"
	implementation "com.auth0:java-jwt:4.0.0"
	implementation "com.auth0:jwks-rsa:0.21.2"

	//Nullable Warning 처리
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

def querydslDir = "src/main/generated/querydsl"

tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

clean {
	delete file(querydslDir)
}

test {
	useJUnitPlatform()
}

war {
	def activeProfile = "local"

	if(project.hasProperty("profile")) {
		activeProfile = profile
	}

	processResources {
		exclude("config")
	}

	into("WEB-INF/classes") {
		from "src/main/resources/config/application-${activeProfile}.yaml"
	}

	rename "application-${activeProfile}.yaml", "application.yaml"

	archiveFileName = "ROOT.war"
}